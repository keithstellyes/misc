=======================================================================================
99 Bottles Of Beer On The Wall 
                                 
AUTHOR: Keith Stellyes
WRITTEN: June 2016            
=======================================================================================
[
n bottles of beer on the wall, n bottles of beer.
Take one down and pass it around, n-1 bottles of beer on the wall.

No more bottles of beer on the wall, no more bottles of beer. 
Go to the store and buy some more, 99 bottles of beer on the wall.


The following chars in the text:
G N T \n , . \s a b d e f h i k l m n o p r s t u w y

most frequent -> less frequent             |only in last verse
b o t l e s \s , f p a w r . \n i h u n d T| k y N m G

                      CELL MAP
  0  |    1               |       2        | 3  - 6 |
COUNT|COUNT_COPY/10s place|RESRVED/1s place|RESERVED|
     |               (count_str)           |

	                  CONSTANTS 
7 |8 |9 |10|11|12|13|14|15|16|17|18|19|20|21|22|23|24|25|26|27|28|29|30|31|32	 
b |o |t |l |e |s |\s|, |f |p |a |w |r |. |\n|i |h |u |n |d |T |k |y |N |m |G	
]

SET INITAL CONSTANTS

>>>>>>>++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++>++++++++++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++++++++++++++++>++++++++++>
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+++++++++++++++++++++++++
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	 
while(COUNT)             :
{                        [
GET COUNT_STRING
   ( 
   COPY COUNT INTO CELL 1 :[>+>+<<-]>>[<<+>>-]
   SET CELL 2 TO 10       :++++++++++<
   CELL1 DIVMOD CELL2     :[>[->+>+<<]>[-<<-[>]>>>[<[-<->]<[>]>>[[-]>>+<]>-<]<<]>>>+<<[-<<+>>]<<<] 
                           >>>>>[-<<<<<+>>>>>]<<<<< 
   CELLS 1 2 += 48        :+++++++++++++++++++++++++++++++++++++++++++++++.
   (and print)                       >++++++++++++++++++++++++++++++++++++++++++++++++.
   
   )
PRINT "bottles of beer on the wall\COMMA :">>>>>>>>>>>.<<<<<<.>.>..>.>.>.>.<<<<<.>>>>>>>.<<.<<<<<<.>>>>..>>>>>>>>.<<<<<<.<<<<<.>>>>>>>>>>>>>>>>>.<<<<<<<<<<<<.<<<<.
                                          >>>>>>>>>>>>>>.<<<<<<<<<<<<.>>.>>>>>.<.<<<<<<<..>>>>.<.
PRINT COUNT_STRING                       :<<<<<<<<<<<<.>.>>>>>>>>>>>.<<<<<<.>.>..>.>.>.>.<<<<<.>>>>>>>.<<.<<<<<<.>>>>..>>>>>>>>.>.>.>>>>>>.<<<<<<<<<<.>>>>>>>>>>>.
                                          <<<<<<<<<<<<<<<<<.>>.<<<<<.>>>>>>>>>>>>>>>>>.<<<<<<<<<<<<<<.>>.>>>>>>>>>>>>>.<<<<<<<<<<<<<<<<<<.>>>>>>>>>>.>>>>>>>.
                                          <<<<<<<<<<<<.>>>>.>>>>>>>>.>.<<<<<<<<<<<<<.>>>.>.<<<<<..>.>>>>>>>>>.<<<<<<<<<<<<<.>>>>.>>>>.>>.<<<<<<<<<<<.
                                          >>>>>>>>>>>>>>>>.>.>.<<<<<<<<<<<<.<.
PRINT "Take one down and pass it around\COMMA"
DECREMENT COUNT ; CLEAR CELLS 1 2        : <<<<<<<<<<<<<->[-]>[-]<<
SET CELL 1 TO NEW_COUNT                  : [>+>+<<-]>>[<<+>>-]<

while(NEW_COUNT)
 {                                      [
   GET COUNT_STRING                     :>++++++++++<
                                          [>[->+>+<<]>[-<<-[>]>>>[<[-<->]<[>]>>[[-]>>+<]>-<]<<]>>>+<<[-<<+>>]<<<]>>>>>[-<<<<<+>>>>>]<<<<< 
   PRINT NEW_COUNT_STRING                :++++++++++++++++++++++++++++++++++++++++++++++++.>++++++++++++++++++++++++++++++++++++++++++++++++.
   PRINT "bottles of beer on the wall\PERIOD >>>>>>>>>>>.<<<<<<.>.>..>.>.>.>.<<<<<.>>>>>>>.<<.<<<<<<.>>>>..>>>>>>>>.<<<<<<.<<<<<.>>>>>>>>>>>>>>>>>.<<<<<<<<<<<<.<<<<.
                                             >>>>>>>>>>>>>>.<<<<<<<<<<<<.>>.>>>>>.<.<<<<<<<..>>>>>>>>>>.>..
CLEAR COUNT_STRING & NEW_COUNT           :<<<<<<<<<<<<<<<<<<<[-]<[-]
 }                                      ]<
CLEAR CELLS 1 2                         
MOVE POINTER TO COUNT                   ]
}

PRINT FINAL VERSE:                      :>>>>>>>>>>>>>>>>>>>>>..>>>>>>>>>.<<<<<<<<<<<<<<<<<<<<<<.>>>>>.>>>>>>>>>>>>>>>>>>.<<<<<<<<<<<<<<<<<<<<<<<.>>>>>>>>>>>.
                                         <<<<<<<<.>>.<<<<<<.>.>..>.>.>.>.<<<<<.>>>>>>>.<<.<<<<<<.>>>>..>>>>>>>>.<<<<<<.<<<<<.>>>>>>>>>>>>>>>>>.<<<<<<<<<<<<.<<<<.
                                         >>>>>>>>>>>>>>.<<<<<<<<<<<<.>>.>>>>>.<.<<<<<<<..>>>>.<.>>>>>>>>>>>>.<<<<<<<<<<<<<<<<<.>>>>>.>>>>>>>>>>>>>>>>>>.
                                         <<<<<<<<<<<<<<<<<<<<<<<.>>>>>>>>>>>.<<<<<<<<.>>.<<<<<<.>.>..>.>.>.>.<<<<<.>>>>>>>.<<.<<<<<<.>>>>..>>>>>>>>.>.>.>>>>>>>>>>>.
                                         <<<<<<<<<<<<<<<<<<<<<<<<.>>>>>.<<<<.<.>>>>>.<<<<.>>>>>>>>>>>>>>.<<<<<<<<<<<<.>>.<.<<<.<.>>>>>>>>>>>.<<<<<<<<.>>.>>>>.>>>>>>>>.>.
                                         <<<<<<<<<<<<<.<<<<<<.>>>>>>>>>>>>>>>>>.>>>>>.<<<<<<<<<<<<<<<<.<.<<<<.>>>>>>>>>>>>>>>>>>>>>>>.<<<<<<<<<<<<<<<<<<<<.>>.
                                         >>>>>>>>>>>>>>>>>>.<<<<<<<<<<<<<<<<<<<<<<<.>>>>>>>>>>>.<<<<<<<<.>>>.<.99>+++++++++++++..-------------<.<<<<<<.>.>..>.>.>.>.<<<<<.
                                         >>>>>>>.<<.<<<<<<.>>>>..>>>>>>>>.<<<<<<.<<<<<.>>>>>>>>>>>>>>>>>.<<<<<<<<<<<<.<<<<.>>>>>>>>>>>>>>.<<<<<<<<<<<<.>>.>>>>>.<.<<<<<<<..
                                         >>>>>>>>>>.
