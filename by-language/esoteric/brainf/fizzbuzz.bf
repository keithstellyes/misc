==========================================================[
Fizz Buzz 

AUTHOR: Keith Stellyes
WRITTEN: June 2016

For numbers 1 through 100, print one of four things:

Fizz if evenly divisible by 3, but not 5
Buzz if by 5, but not 3
FizzBuzz if both
The number itself if none of the above.
==========================================================]

SET STRINGS
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
>++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+++++++++++++++++++++++++++++++++++>++++++++++++++++++++++++++++++++++
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+++++++++++++++++>++++++++++++++++++++++++++++++++++++++++++++++++++++
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
>++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>++
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
++++++++++++++++++++++++++++++++++++++++++++>+++++++++++++++++++++++++
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
++++++++++++++++++++++++++>+++++++++++++++++++++++++++++++++++++++++++
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
++++++++> SET COUNT_100 ++++++++++++++++++++++++++++++++++++++++++++++
++++++++++++++++++++++++++++++++++++++++++++++++++++++

                       CELL MAPS			   
     STRINGS   |           |          DIVISION         |
0|1|2|3|4|5|6|7| 8   | 9   |    10    |   11  |12 to 15|
F|I|Z|Z|B|U|Z|Z|C_100|COUNT|COUNT_TEMP|DIVISOR|RESERVED|
                           
|     FLAGS    |                                   
| 18 | 19 | 20 |
|FIZZ|BUZZ|NORM|

[
FLAG_IS_NOT_FIZZ = FALSE (CELL 18)  : >>>>>>>>>>[-]
FLAG_IS_NOT_BUZZ = FALSE (CELL 19)  : >[-]
FLAG_IS_NORM = FALSE (CELL 20)      : >+
INC COUNT(CELL 9)                   : <<<<<<<<<<<+

_CHECK COUNT%3_
COPY COUNT TO CELL 10           : [->+>+<<]>>[-<<+>>]<
SET DIVISOR TO 3 AT CELL 11     : >+++< 
STORE COUNT%3 AT CELL 11        : [>[->+>+<<]>[-<<-[>]>>>[<[-<->]<[>]>>[[-
                                  ]>>+<]>-<]<<]>>>+<<[-<<+>>]<<<] 
                                  >>>>>[-<<<<<+>>>>>]<<<<< 
IF (COUNT % CONST_3 GREATER 0)         
{                               : >[
FLAG_IS_NOT_FIZZ = TRUE;        : >>>>>>>[-]+<<<<<<<
CLEAR DIVISOR                   : [-]
}                               : ]
CLEAR COUNT_TEMP                : <[-]<

_CHECK COUNT%5_ 
COPY COUNT TO CELL 10           : [->+>+<<]>>[-<<+>>]<<
SET DIVISOR TO 5 AT CELL 11     : >>+++++<
STORE COUNT%5 AT CELL 11        : [>[->+>+<<]>[-<<-[>]>>>[<[-<->]<[>]>>[[-
                                  ]>>+<]>-<]<<]>>>+<<[-<<+>>]<<<] 
                                  >>>>>[-<<<<<+>>>>>]<<<<< 
IF (COUNT % CONST_5 GREATER 0):       
{                               : >[
FLAG_IS_NOT_BUZZ = TRUE;        : >>>>>>>>[-]+<<<<<<<<
CLEAR DIVISOR;                  : [-]
}                               : ]
CLEAR COUNT_TEMP                : <[-]

IF NOT(FLAG_IS_NOT_FIZZ)        : >>>>>>>>-
{                               : [
PRINT FIZZ : <<<<<<<<<<<<<<<<<<.>.>.>.>>>>>>>>>>>>>>>
FLAG_IS_NORM = FALSE            : >>[-]<<[-]
}                               : ]
___PTR SHOULD BE AT 18 HERE___
IF NOT(FLAG_IS_NOT_BUZZ)        
{                               : >-[
PRINT BUZZ : <<<<<<<<<<<<<<<.>.>.>.>>>>>>>>>>>>
FLAG_IS_NORM = FALSE >[-]<[-]
}                               : ]
___PTR SHOULD BE AT 19 HERE___
IF (FLAG_IS_NORM)            
{                               : >[
PRINT COUNT                       COPY COUNT<<<<<<<<<<<[>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<-]>>>>>>>>>>>>>>[<<<<<<<<<<<<<
                                  <+>>>>>>>>>>>>>>-]<CHECK 100s PLACE[>+>+<<-]>>[<<+>>-]++++++++++++++++++++++++++++
                                  ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++COUNT DIVM
								  OD100<[>[->+>+<<]>[-<<-[>]>>>[<[-<->]<[>]>>[[-]>>+<]>-<]<<]>>>+<<[-<<+>>]<<<]>>>>>
								  [-<<<<<+>>>>>]<<<++++++++++REMAINDER DIVMOD 10<[>[->+>+<<]>[-<<-[>]>>>[<[-<->]<[>]
								  >>[[-]>>+<]>-<]<<]>>>+<<[-<<+>>]<<<]>>>>>[-<<<<<+>>>>>]<<<<<<PRINT RESULT A B REMA
								  INDER++++++++++++++++++++++++++++++++++++++++++++++++.[-]>++++++++++++++++++++++++
								  ++++++++++++++++++++++++.[-]>++++++++++++++++++++++++++++++++++++++++++++++++.[-]<
}                                 <<[-]<<[-]]
___PTR SHOULD BE AT 20 HERE___ 
PRINT newline                            : ++++++++++.[-]
DECREMENT COUNT_100                      : <<<<<<<<<<<<-
}                                        : ]
